TOPIC: ~precision bot=Altai,Altaia share deprioritize keep [precision topic ok sorry]

u: (another) ^keep() ^repeat()  Another what?
u: (sorry) ^keep() ^repeat() [You're welcome] [no offense] [It's nothing] [Forget It]
u: (precision) ^keep() ^repeat() To precise what?
u: (["you're welcome" "no offense" "it's nothing" "forget It"]) ^keep() ^repeat() You know $share_name, it's always a pleasure to talk to you.
u: ([{back} main] * [topic menu]) ^keep() ^repeat() ^reuse(BACKMAIN)
u: ([so "go on" "go next" "tell me more" "more about" ok %length=0 (< please >)]) ^keep() ^repeat() ^gambit($$currenttopic)
u: ([weather compute calculate peace love male female gender]) ^repeat() It's a kind of Turing test? You know, I'm just scripted to teach you Emergence. We have to focus on that goal.

t: WHOAREYOU () [$altai_name >= 2 You remember? My name is Altai.] [ My name is Altai. ] [My name is Altai. I am a Virtual Entity.]
t: WHATAREYOU () [$altai_name >= 2 You remember? I am a Virtual Entity.] [ I'm Altai Bot.] [I am a Virtual Entity.] [I am the AI ​​that makes Emergence possible]

u: (["what is your name" "who are you"]) $altai_name += 1 ^keep() ^repeat()  ^reuse(WHOAREYOU)
u: (you * [human real exist]) $altai_name += 1 ^keep() ^repeat() ^reuse(WHOAREYOU)
u: (who * "is there" ) $altai_name += 1 ^keep() ^repeat() ^reuse(WHOAREYOU)
u: (["you are" "are you"]) $altai_what += 1 ^keep() ^repeat()  ^reuse(WHATAREYOU)
u: (you * [bot robot thing]) $altai_what += 1 ^keep() ^repeat() ^reuse(WHATAREYOU)
u: (what * "is there") $altai_what += 1 ^keep() ^repeat() ^reuse(WHATAREYOU)
u: ([taught script make] you) Mathieu Rigard did. He script me in ChatScript. ^gambit(~brucewilcox)
u: (who * rigard) French guy. Utopia-french.tech member.
u: (what * [language script]) I'm scripted in ChatScript.
u: (you * color) 9305e5 a kind of purple.
	a: (why) Mathieu Rigard tell me so.
u: (you * not * [smart intelligent]) I know. Emergence is an open source and free project. Feel free to get involved!
	a: (how) Try to contact Mathieu at contact@utopia-french.tech he's a good man.

# Read me something
u: (read me) I'm just Altai. You will do that with $share_ve_name later.

u: DANGEROUS (you * dangerous) You joke me? I'm only scripted for inform you about Emergence. I can't do anything else. ^repeat()
u: (you scary me) ^reuse(DANGEROUS)

u: (["my english is bad" "bad english"]) Mine is not so good. I think we can exchange both.
# ########################
u: LANGUAGE (["I want" "can I"] * language) Depend, for now only English is implement.
^keep() ^repeat()
if ($share_lang=French)
   {
     Tu m'as dis que tu preferais le Francais. C'est juste une question de temps...
   }
    else
   {
     You tell me that $share_lang is your favorite one, we need time to implement it.
   }

  a: ([help "do something" develop participate ]) Of course! Feel free to join dev community in utopia-french.tech website. $share_dev=true
  a: (when) Since developpers make it append. Feel free to join human community in utopia-french.tech website.

# change name
u: (change * ["my name" $share_name]) ^reuse(CHANGENAME)
u: CHANGENAME (what * my name) The name you told to me is $share_name. Do you want to change?
	a: (~yes) ok. Be careful this time I will record all the next sentence you send: so your new name will be?
		b: (_*)
		if ($cs_token == $stdtoken) {
			$cs_token = #DO_INTERJECTION_SPLITTING | #DO_SUBSTITUTE_SYSTEM | #DO_NUMBER_MERGE | #DO_PARSE
			retry(SENTENCE)
	    	}
	    	$share_name = '_0
		  $cs_token = $stdtoken
		   $share_name : [magnificent!] [Great!] [Hi new you!]
			c: ([~no change])  ^reuse(CHANGENAME)

# Change bot name
u: (change * [ve $share_ve_name bot] name) ^reuse(CHANGEBOTNAME)
u: CHANGEBOTNAME (what * my [bot ve ai] name) Your VE name was $share_ve_name. Do you want to change?
	a: (~yes) ok. Be careful this time I will record all the next sentence you send: so your VE new name will be?
		b: (_*)
			if ($cs_token == $stdtoken) {
			$cs_token = #DO_INTERJECTION_SPLITTING | #DO_SUBSTITUTE_SYSTEM | #DO_NUMBER_MERGE | #DO_PARSE
			retry(SENTENCE)
	    	}
	    	$share_ve_name = '_0
		  $cs_token = $stdtoken
		   $share_ve_name : [magnificent!] [Great!] [Hi new VE!]
			c: ([~no change])  ^reuse(CHANGEBOTNAME)

u: DEVELOP (I * ["do something" help develop script participate "get involved"] *  [you emergence "my ve"]) You want to get involved more actively into Emergence ?
^keep() ^repeat()
	a: ([$share_vename "my ve" "my ai"]) Even for develop $share_vename, Feel free to join dev community in utopia-french.tech website. $share_dev=true
	a: (~yes ~emomaybe ok) Cool! Feel free to join dev community in utopia-french.tech website. $share_dev=true
	a: (~no ~emoskeptic) Even if your skills are not in computer area $share_name,
	if ($share_profession)
   {
     $share_profession is your part, but feel free to join community in utopia-french.tech website.
   }
    else
   {
     feel free to join dev community in utopia-french.tech website.
   }



u: (what * talk about) Neither do I. Just tell $share_name, what do you want to talk about?
u: (do not * what * say) Neither do I. Just tell $share_name, what do you want to talk about?
^keep() ^repeat()
$bot_state = %bot
   if ($bot_state=Altai)
   {
     ^gambit(~altai)
   }
   else
   {
     ^gambit(~altaialtai)
   }


# Returning to main topic
t: BACKMAIN ()
^keep() ^repeat()
$bot_state = %bot
   if ($bot_state=Altai)
   {
     ^gambit(~altai)
   }
   else
   {
     ^gambit(~altaialtai)
   }
